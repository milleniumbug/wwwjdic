buildscript {  
  repositories {
    jcenter()
    mavenLocal()
  }
  dependencies {
    classpath 'com.android.tools.build:gradle:2.3.3'
  }
}

apply plugin: 'com.android.application'

repositories {  
  mavenCentral()
  mavenLocal()
}

dependencies {  
  compile fileTree(dir: 'libs', include: '*.jar')
  compile project(':wwwjdic')
}

android {  
  compileSdkVersion 19
  buildToolsVersion '25.0.0'
  enforceUniquePackageName = false

  defaultConfig {
    versionName "2.4.0"
  }

  dexOptions {
    preDexLibraries = false
  }

  signingConfigs {
    release {
      storeFile file ("nick.keystore")
    }
   }

   buildTypes {
     release {
       signingConfig signingConfigs.release
     }
   }


  sourceSets {
    debug.setRoot('build-types/debug')
    release.setRoot('build-types/release')

    main {
      manifest.srcFile 'AndroidManifest.xml'
      java.srcDirs = ['src']
      resources.srcDirs = ['src']
      aidl.srcDirs = ['src']
      renderscript.srcDirs = ['src']
      res.srcDirs = ['res']
      assets.srcDirs = ['assets']
    }
  }
  android.applicationVariants.all { variant ->
    variant.outputs.each { output ->
      output.outputFile = new File(
      output.outputFile.parent,
      output.outputFile.name.replace(".apk", "-${variant.versionName}.apk"))
    }
  }

  lintOptions {
    checkReleaseBuilds true
    abortOnError false
  }
}

if (project.hasProperty('storePassword')
        && project.hasProperty('keyAlias')
        && project.hasProperty('keyPassword')
        && project.hasProperty('storeFile')) {
    android.signingConfigs.release.storeFile = file storeFile
    android.signingConfigs.release.storePassword = storePassword
    android.signingConfigs.release.keyAlias = keyAlias
    android.signingConfigs.release.keyPassword = keyPassword
}
